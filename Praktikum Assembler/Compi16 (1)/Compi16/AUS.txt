; Quelltext des Moduls AUS.TXT
       ENT    UPAUS     ;symb. Adresse UPAUS auch in anderen
                        ;Modulen bekannt machen
UPAUS  STA    ZAHL      ;Akkumulator speichern
       LDA    TABMERK   ;Anfangsadresse der Tabelle holen
       STA    TAB       ;und zur weiteren Berechnung ablegen
       LDA    ZAHL      ;auszugebende Zahl in den Akkumulator laden
ZYKL1  CMP    ZEHN      ;Test, ob Zahl kleiner als Zehn ist
       JLT    AUS1      ;ja, also Sprung zum Ausgeben
                        ;nein, noch größer oder gleich Zehn, also zyklisch
                        ;Zehn subtrahieren und dabei Tabellenzeiger erhöhen
       LDA    TAB       ;Tabellenzeiger holen
       ADD    EINS      ;Zeiger auf nächste Ziffer setzen
       STA    TAB       ;veränderten Zeiger wieder abspeichern
       LDA    ZAHL      ;Zahl holen
       SUB    ZEHN      ;Zehn abziehen
       STA    ZAHL      ;und wieder abspeichern
       JMP    ZYKL1     ;diesen Zyklus solange ausführen, bis Zahl
                        ;kleiner als Zehn ist
                        ;Ausgabe
AUS1   LDA,I  TAB       ;über Tabellenzeiger auszugebenden ASCII-Code
                        ;holen
       WRA              ;ASCII-Code der Zehnerstelle ausgeben
       LDA    TABMERK   ;Anfangsadresse der Tabelle holen
       ADD    ZAHL      ;Tabellenzeiger um den Wert der Einerstelle
                        ;erhöhen
       STA    TAB       ;Tabellenzeiger für indirekte Adressierung
                        ;speichern
       LDA,I  TAB       ;über Tabellenzeiger auszugebenden ASCII-Code
                        ;holen
       WRA              ;ASCII-Code der Einerstelle ausgeben
       RET              ;finales Return
;Definitionen
ZAHL   RST    1         ;Speicher für auszugebende Zahl
EINS   DEC    1         ;dezimale Eins für Zeigerrechnungen
ZEHN   DEC    10        ;dezimale Zehn für Abtrennung der Zehnerstelle
                        ;von Zahl
TAB    RST    1         ;Speicherzelle für Tabellenzeiger

TABMERK DEF   TABANF    ;Ablage für Tabellenanfangsadresse
TABANF HEX    30        ;Tabelle der ASCII-Codes der Ziffern von Null
                        ;bis Neun
       HEX 31           ;ASCII-Code von Eins
       HEX 32           ;
       HEX 33           ;
       HEX 34           ;
       HEX 35           ;
       HEX 36           ;
       HEX 37           ;
       HEX 38           ;
       HEX 39           ; ASCII-Code von Neun